apply plugin: "cpp"

ext.libroot = new File(rootProject.rootDir, "libs")
ext.gtest_root = new File(libroot, "googletest/googletest")
ext.gbench_root = new File(libroot, "googlebench")
ext.eigen_root = new File(libroot, "eigen")

model {
    buildTypes {
        release
        debug
    }
    components {
        googleTest(NativeLibrarySpec) {
            sources.cpp {
                source {
                    srcDir new File(gtest_root, "src")
                    include "**/gtest-all.cc"
                }
                exportedHeaders {
                    srcDirs gtest_root, new File(gtest_root, "include")
                    include "**/*.hpp", "**/*.h"
                }
            }
        }

        googleBench(NativeLibrarySpec) {
            binaries.all {
                if (toolChain instanceof Gcc || toolChain instanceof Clang) {
                    cppCompiler.args << '-std=c++14'
                    cppCompiler.args.remove('-Werror')
                    cppCompiler.args << '-Wall'
                    linker.args << '-pthread'
                } else {
                    cppCompiler.args << '/std:c++14'
                    linker.args << 'shlwapi.lib'
                }
            }

            sources.cpp {
                source {
                    srcDir new File(gbench_root, "src")
                    include "**/*.cc"
                    exclude "**/benchmark_main.cc"
                }
                exportedHeaders {
                    srcDirs new File(gbench_root, "src"), new File(gbench_root, "include")
                    include "**/*.hpp", "**/*.h"
                }
                lib project: ':libs', library: "googleTest", linkage: "static"
            }
        }
    }
    repositories {
        libs(PrebuiltLibraries) {
            eigen {
                headers.srcDir eigen_root
                headers.include "**/*"
            }
        }
    }
}