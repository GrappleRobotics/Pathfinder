import groovy.json.JsonOutput

Map<String, Object> vendorJson() {
  return [
    fileName: "Pathfinder.json",
    name: "Pathfinder",
    version: project.version,
    uuid: "44237bb3-7675-43ba-894a-302083a37bd8",
    mavenUrls: [
      "https://dev.imjac.in/maven"
    ],
    jsonUrl: "https://dev.imjac.in/maven/grpl/pathfinder/Pathfinder-latest.json",
    cppDependencies: [
      [
        groupId: project.group,
        artifactId: "Pathfinder",
        version: project.version,
        libName: "Pathfinder",
        configuration: "native_pathfinder",
        headerClassifier: "headers", 
        binaryPlatforms: []
      ],
      [
        groupId: "grpl.thirdparty.tuxfamily.eigen",
        artifactId: "Eigen",
        version: project(':libs').eigen_version,
        libName: "Eigen",
        configuration: "native_pathfinder",
        headerClassifier: "headers",
        binaryPlatforms: []
      ]
    ],
    javaDependencies: [
      [
        groupId: project.group,
        artifactId: "Pathfinder-Java",
        version: project.version
      ]
    ],
    jniDependencies: [
      [
        groupId: project.group,
        artifactId: "Pathfinder-JNI",
        version: project.version,
        isJar: true,
        skipInvalidPlatforms: true,
        validPlatforms: [
          "linuxx86-64",
          "windowsx86-64",
          "osxx86-64",
          "linuxx86",
          "windowsx86",
          "osxx86",
          "linuxathena",
          "linuxraspbian"
        ]
      ]
    ]
  ]
}

String vendorJsonString() {
  return JsonOutput.prettyPrint(JsonOutput.toJson(vendorJson()))
}

task generateVendorDepsJson() {
  def outfile = new File(buildDir, "Pathfinder.json")
  outputs.file(outfile)

  doLast {
    outfile.text = vendorJsonString()
  }
}

publishing {
  publications {
    vendordeps(MavenPublication) {
      artifactId 'Pathfinder-FRCDeps'
      artifact(generateVendorDepsJson.outputs.files.files[0]) {
        builtBy generateVendorDepsJson
      }
    }
  }
}

afterEvaluate {
  publishing.repositories.all {
    def vendorTask = task "writeLatestVendorDepsTo${name}"() {
      def outfile = new File(new File(url), "grpl/pathfinder/Pathfinder-latest.json")
      outputs.file(outfile)

      doLast {
        outfile.text = vendorJsonString()
      }
    }

    tasks.withType(PublishToMavenRepository).all {
      dependsOn vendorTask
    }
  }
}