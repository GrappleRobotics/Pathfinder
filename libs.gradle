project(":libs") {
    apply plugin: "cpp"

    ext.libroot = new File(rootProject.rootDir, "libs")
    ext.gtest_root = new File(libroot, "googletest/googletest")
    ext.units_root = new File(libroot, "units")
    ext.eigen_root = new File(libroot, "eigen")

    model {
        components {
            googleTest(NativeLibrarySpec) {
                sources.cpp {
                    source {
                        srcDir new File(gtest_root, "src")
                        include "**/gtest-all.cc"
                    }
                    exportedHeaders {
                        srcDirs gtest_root, new File(gtest_root, "include")
                        include "**/*.hpp", "**/*.h"
                    }
                }
            }
        }
        repositories {
            libs(PrebuiltLibraries) {
                units {
                    headers.srcDir new File(units_root, "src")
                    headers.include "**/*.h"
                }
                eigen {
                    headers.srcDir eigen_root
                    headers.include "**/*"
                }
            }
        }
    }
}